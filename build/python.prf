# profile to include and link Python

# Change this variable to your python version (2.6, 2.7, 3.3, ...)
win32:PYTHON_VERSION=35
unix:PYTHON_VERSION=UNKNOWN_BECAUSE_DETECT_FAILED

win32 {
  # for windows install a Python development kit or build Python yourself from the sources
  # Make sure that you set the environment variable PYTHON_PATH to point to your
  # python installation (or the python sources/header files when building from source).
  # Make sure that you set the environment variable PYTHON_LIB to point to
  # the directory where the python libs are located.
  #
  # When using the prebuild Python installer, this will be:
  # set PYTHON_PATH = c:\Python26
  # set PYTHON_LIB  = c:\Python26\libs
  #
  # When using the python sources, this will be something like:
  # set PYTHON_PATH = c:\yourDir\Python-2.6.1\
  # set PYTHON_LIB  = c:\yourDir\Python-2.6.1\PCbuild8\Win32
  isEmpty(PYTHON_PATH):PYTHON_PATH=$$(PYTHON_PATH)
  isEmpty(PYTHON_LIB):PYTHON_LIB=$$(PYTHON_LIB)
  isEmpty(PYTHON_PATH):error("Set PYTHON_PATH, please")
  isEmpty(PYTHON_LIB):error("Set PYTHON_LIB, please")
  INCLUDEPATH += $$PYTHON_PATH/PC $$PYTHON_PATH/include
  LIBS += -L$$PYTHON_LIB -lpython$${PYTHON_VERSION} #$${DEBUG_EXT}

} else:unix {

  # workaround Qt issue https://bugreports.qt.io/browse/QTBUG-56164
  macx:QT_CONFIG -= no-pkg-config
  # pkg-config is used to autodetect Python.
  # make sure that you have installed a matching pythonX-dev package.
  CONFIG *= link_pkgconfig
  PYTHON_PKG = python-3.5
  PKGCONFIG *= $${PYTHON_PKG}
  PYTHON_VERSION_AUTODETECTED=$$system($$pkgConfigExecutable() --modversion $${PYTHON_PKG})
  !isEmpty(PYTHON_VERSION_AUTODETECTED) {
      PYTHON_VERSION=$$PYTHON_VERSION_AUTODETECTED
  } else:macx {
    # No `brew`, ok, then you need to have the Python development kit installed as framework
    INCLUDEPATH += /System/Library/Frameworks/Python.framework/Headers
    LIBS += -F/System/Library/Frameworks -framework Python
    PYTHON_VERSION=3.5
  }
}
