# profile to include and link Python

# Change this variable to your python version (3.5, 3,7, 3.8, ...)
isEmpty(PYTHON3_VERSION_MINOR):PYTHON3_VERSION_MINOR=7
unix:PYTHON_VERSION=UNKNOWN_BECAUSE_DETECT_FAILED

win32 {
  # for windows install a Python development kit or build Python yourself from the sources
  # Make sure that you set the environment variable PYTHON_PATH to point to your
  # python installation (or the python sources/header files when building from source).
  # Make sure that you set the environment variable PYTHON_LIB to point to
  # the directory where the python libs are located.
  #
  # When using the prebuild Python installer, this will be:
  # set PYTHON_PATH = c:\Python26
  # set PYTHON_LIB  = c:\Python26\libs
  #
  # When using the python sources, this will be something like:
  # set PYTHON_PATH = c:\yourDir\Python-2.6.1\
  # set PYTHON_LIB  = c:\yourDir\Python-2.6.1\PCbuild8\Win32
  isEmpty(PYTHON_PATH):PYTHON_PATH=$$(PYTHON_PATH)
  isEmpty(PYTHON_PATH)|!exists($$PYTHON_PATH):error("Set correct PYTHON_PATH, please")
  isEmpty(PYTHON_LIB):PYTHON_LIB=$$(PYTHON_LIB)
  isEmpty(PYTHON_LIB):!isEmpty(PYTHON_PATH) {
       PYTHON_LIB=$$clean_path($${PYTHON_PATH})/libs
       message("PYTHON_PATH was set, setting PYTHON_LIB to $$PYTHON_LIB")
  }
  isEmpty(PYTHON_LIB)|!exists($$PYTHON_LIB):error("Set correct PYTHON_LIB, please")
  PYTHON_VERSION=$$system($$system_path($$PYTHON_PATH/python.exe) -c $$system_quote(import sys;(v1,v2,_,_,_)=sys.version_info;print('"'%s%s'"'%(v1,v2))))
  message(PYTHON_VERSION is detected as $$PYTHON_VERSION)
  INCLUDEPATH += $$PYTHON_PATH/PC $$PYTHON_PATH/include
  LIBS += -L$$PYTHON_LIB -lpython$${PYTHON_VERSION}
} else:unix {

  # workaround Qt issue https://bugreports.qt.io/browse/QTBUG-56164
  macx:QT_CONFIG -= no-pkg-config
  # pkg-config is used to autodetect Python.
  # make sure that you have installed a matching pythonX-dev package.
  CONFIG *= link_pkgconfig
  PYTHON_VERSION_AUTODETECTED = $$system($$pkgConfigExecutable() --modversion python3)
  PYTHON_VERSION = $$system($$pkgConfigExecutable() --modversion python-3.$${PYTHON3_VERSION_MINOR})
  isEmpty(PYTHON_VERSION): !isEmpty(PYTHON_VERSION_AUTODETECTED) {
      PYTHON_VERSION=$$PYTHON_VERSION_AUTODETECTED
  }
  PYTHON_PKG = python-$${PYTHON_VERSION}
  equals(PYTHON_VERSION, 3.8) | versionAtLeast(PYTHON_VERSION, 3.8) { # versionAtLeast requires Qt 5.10+
          PYTHON_PKG = $${PYTHON_PKG}-embed
  }
  PKGCONFIG *= $${PYTHON_PKG}
  linux {
          LIBS *= $$system($$pkgConfigExecutable() --libs --static $${PYTHON_PKG})
  }
} else:macx {
    # No `brew`, ok, then you need to have the Python development kit installed as framework
    INCLUDEPATH += /System/Library/Frameworks/Python.framework/Headers
    LIBS += -F/System/Library/Frameworks -framework Python
    PYTHON_VERSION=3.$${PYTHON3_VERSION_MINOR}
}
